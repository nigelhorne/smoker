#!/usr/bin/env perl

# The smokerupdate program broadcasts updates on port 21212. Forward the
# update notifications to the docker images on the local machine.  In this
# example the docker image is listening on port 21214 (see *dockerfile and
# *install)

use strict;
use warnings;
use IO::Socket::INET;
use autodie qw(:all);

my $sin = IO::Socket::INET->new(LocalPort => 21212, Proto => 'udp')
	or die "Can't listen on port 21212";

while(1) {
	get_dists();
}

sub get_dists {
	my $dists;

	while(1) {
		my $line;

		$sin->recv($line, 256);

		print $line;

		$dists .= $line;

		last if($line eq "\n");
	}

	open(my $pin, '-|', '/usr/bin/docker ps');

	my @ports;
	my $line = <$pin>;
	while($line = <$pin>) {
		if($line =~ /0.0.0.0:(\d+)->\d+\/udp/) {
			push @ports, $1;
		}
	}

	close($pin);

	die "Couldn't detect any smokers\n" if(scalar(@ports) == 0);

	foreach my $port(@ports) {
		print "Send to $port:$dists\n";
		my $sout = IO::Socket::INET->new(PeerPort => $port,
				PeerAddr => '127.0.0.1',
				Proto => 'udp')
			or die "Can't send to port $port";

		# TODO: break up if the dists is a large packet
		$sout->send($dists);

		close($sout);
	}
}
